#!/usr/bin/env zsh
#
# A theme based on trongrg theme
#  * RVM/Rbenv info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#
# Authors:
# Trong Tran trongrg@gmail.com

pmodload 'helper'

function prompt_trongrg_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_trongrg_pwd="$MATCH"
    unset MATCH
  else
    _prompt_trongrg_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_trongrg_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get ruby information
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  # Format PWD.
  prompt_trongrg_pwd

  local prompt_line_1
  prompt_trongrg_setup_prompt

  PROMPT="$prompt_line_1$prompt_new_line$prompt_chars "
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_trongrg_setup_prompt () {
  local prompt_line_1a_width=${#${(S%%)prompt_line_1a//(\%([KF1]|)\{*\}|\%[Bbkf])}}
  local prompt_line_1b_width=${#${(S%%)prompt_line_1b//(\%([KF1]|)\{*\}|\%[Bbkf])}}
  local git_info_width=${#${(S%%)git_info[prompt]//(\%([KF1]|)\{*\}|\%[Bbkf])}}

  local prompt_padding_size=$(( COLUMNS
                                  - prompt_line_1a_width
                                  - prompt_line_1b_width
                                  - git_info_width
                                  - 1 ))

  # Try to fit in long path and user@host.
  if (( prompt_padding_size > 0 )); then
    local prompt_padding
    eval "prompt_padding=\${(l:${prompt_padding_size}:: :)_empty_zz}"
    prompt_line_1="$prompt_line_1a$git_info$prompt_padding$prompt_line_1b"
    return
  fi

  prompt_padding_size=$(( COLUMNS - prompt_line_1a_width ))

  # Didn't fit; try to fit in just long path.
  if (( prompt_padding_size > 0 )); then
    local prompt_padding
    eval "prompt_padding=\${(l:${prompt_padding_size}:: :)_empty_zz}"
    prompt_line_1="$prompt_line_1a$git_info$prompt_padding"
    return
  fi

  # Still didn't fit; truncate
  local prompt_pwd_size=$(( COLUMNS - 5 ))
  prompt_line_1="$prompt_gfx_tbox$prompt_l_paren%B%F{$prompt_adam2_color2}%$prompt_pwd_size<...<%~%<<$prompt_r_paren%b%F{$prompt_adam2_color1} "
}

function prompt_trongrg_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  __PROMPT_COLORS=(
    "%F{81}"  # turquoise
    "%F{166}" # orange
    "%F{135}" # purple
    "%F{161}" # hotpink
    "%F{118}" # limegreen
  )

  __GIT_COLORS=(
    "%F{81}"  # branch
    "%F{118}" # ahead clean
    "%F{161}" # behind
    "%F{133}" # dirty
    "%F{082}" # unmerged added renamed
    "%F{160}" # deleted
    "%F{166}" # modified
    "%F{136}" # untracked
  )

  # git
  zstyle ':prezto:module:git:info:keys' format 'prompt' '($(coalesce "%b" "%p" "%c")%s%A%B%D%C%U)%a%d%m%u%r'

  zstyle ':prezto:module:git:info:branch' format "${__GIT_COLORS[1]}%b%f"
  zstyle ':prezto:module:git:info:ahead' format "${__GIT_COLORS[2]} ⬆%f"
  zstyle ':prezto:module:git:info:behind' format "${__GIT_COLORS[3]} ⬇%f"
  zstyle ':prezto:module:git:info:dirty' format "${__GIT_COLORS[4]} ✘%f"
  zstyle ':prezto:module:git:info:clean' format "${__GIT_COLORS[2]} ✔%f"
  zstyle ':prezto:module:git:info:unmerged' format "${__GIT_COLORS[5]} ═%f"

  zstyle ':prezto:module:git:info:added' format "${__GIT_COLORS[5]} ✚%f"
  zstyle ':prezto:module:git:info:deleted' format "${__GIT_COLORS[6]} ✖%f"
  zstyle ':prezto:module:git:info:modified' format "${__GIT_COLORS[7]} ✹%f"
  zstyle ':prezto:module:git:info:untracked' format "${__GIT_COLORS[8]} ✭%f"
  zstyle ':prezto:module:git:info:renamed' format "${__GIT_COLORS[5]} ➜%f"

  # ruby info (rvm, rbenv)
  zstyle ':prezto:module:ruby:info:version' format '[%v]'

  prompt_user_host="${__PROMPT_COLORS[3]}%n%f@${__PROMPT_COLORS[2]}%m%f"
  prompt_pwd="${__PROMPT_COLORS[5]}"'$_prompt_trongrg_pwd'"%f"

  prompt_line_1a="$prompt_user_host $prompt_pwd "
  prompt_line_1b='%F{blue}$ruby_info[version]%f'
  prompt_chars=" %B${__PROMPT_COLORS[2]}❯${__PROMPT_COLORS[1]}❯${__PROMPT_COLORS[5]}❯%f%b"

  add-zsh-hook precmd prompt_trongrg_precmd
}

prompt_trongrg_setup "$@"
